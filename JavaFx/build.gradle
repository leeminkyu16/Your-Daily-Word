plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
    id 'io.miret.etienne.sass' version '1.5.0'
}

group 'com.minkyu.yourdailyword'
version '1.0.0'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.minkyu.yourdailyword.javafx'
    mainClass = 'com.minkyu.yourdailyword.javafx.Main'
}

javafx {
    version = '17'
    modules = ['javafx.controls', 'javafx.fxml']
}

sass {
    directory = file("${rootDir}/.gradle/sass-cache")
    noAutoCopy()
}

project(':Your-Daily-Word-Common').tasks.compileSass {
    enabled = false
}

tasks.compileSass {
    sourceDir = file("${rootDir}/src/main/resources/com/minkyu/yourdailyword/javafx/scss")
    outputDir = file("${buildDir}/resources/main/com/minkyu/yourdailyword/javafx")
    destPath = "css"

    style = "expanded"

    sourceMap = none
    quiet()
}

jar {
    dependsOn(tasks.compileSass)
}

dependencies {
    implementation group: 'com.ibm.icu', name: 'icu4j', version: '74.1'
    implementation group: 'com.google.inject', name: 'guice', version: '7.0.0'
    implementation group: 'com.google.inject.extensions', name: 'guice-assistedinject', version: '7.0.0'
    implementation group: 'io.reactivex.rxjava3', name: 'rxjava', version: '3.1.8'
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.21.12'
    implementation project(":Your-Daily-Word-Common")
    implementation 'org.jetbrains:annotations:24.0.0'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Your Daily Word'
    }

    jpackage {
        icon = './assets/Icon.ico'
    }
}

jlinkZip {
    group = 'distribution'
}
