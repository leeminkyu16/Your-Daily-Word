plugins {
	id 'java'
	id 'application'
	id 'org.javamodularity.moduleplugin' version '1.8.12'
	id 'org.openjfx.javafxplugin' version '0.0.13'
	id 'org.beryx.jlink' version '2.25.0'
	id 'io.miret.etienne.sass' version '1.5.0'
}

group 'com.minkyu.yourdailyword'
version '1.0.0'

repositories {
	mavenCentral()
}

ext {
	junitVersion = '5.10.3'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

application {
	mainModule = 'com.minkyu.yourdailyword.javafx'
	mainClass = 'com.minkyu.yourdailyword.javafx.Main'
}

javafx {
	version = '17'
	modules = ['javafx.controls', 'javafx.fxml', 'javafx.swing']
}

sass {
	directory = file("${rootDir}/.gradle/sass-cache")
	noAutoCopy()
}

project(':Your-Daily-Word-Common').tasks.compileSass {
	enabled = false
}

tasks.compileSass {
	sourceDir = file("${rootDir}/src/main/resources/com/minkyu/yourdailyword/javafx/scss")
	outputDir = file("${buildDir}/resources/main/com/minkyu/yourdailyword/javafx")
	destPath = "css"

	style = "expanded"

	sourceMap = none
	quiet()
}

jar {
	dependsOn(tasks.compileSass)

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {
	implementation group: 'com.ibm.icu', name: 'icu4j', version: '74.1'

	implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '1.9.10'

	implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '4.27.0'
	implementation group: 'com.google.protobuf', name: 'protobuf-java-util', version: '4.27.0'

	implementation group: 'io.grpc', name: 'grpc-okhttp', version: '1.66.0'
	implementation group: 'io.grpc', name: 'grpc-protobuf', version: '1.66.0' exclude group: 'javax.annotation', module: 'javax.annotation-api'
	implementation group: 'io.grpc', name: 'grpc-stub', version: '1.66.0'
	implementation group: 'org.jetbrains', name: 'annotations', version: '24.1.0'

	implementation group: 'com.google.inject', name: 'guice', version: '7.0.0'
	implementation group: 'com.google.inject.extensions', name: 'guice-assistedinject', version: '7.0.0'

	implementation group: "com.google.zxing", name: "core", version: "3.5.3"

	implementation group: 'io.reactivex.rxjava3', name: 'rxjava', version: '3.1.8'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.2'
	implementation project(":Your-Daily-Word-Common")

	implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.24.0'

	testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
	testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
	useJUnitPlatform()
}

jlink {
	imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
	options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

	forceMerge('kotlin')

	launcher {
		name = 'Your Daily Word'
	}

	jpackage {
		icon = './assets/Icon.ico'
	}
}

jlinkZip {
	group = 'distribution'
}
